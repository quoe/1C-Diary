////////////////////////////////////////////////////////////////////////////////
// ОбменМобильноеПриложениеВызовСервераПовтИсп: кешируемые функции по 
//			обслуживанию плана обмена "МобильноеПриложение"
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает массив узлов плана обмена, у которых снят признак активности
//	
Функция ПолучитьНеактивныеУзлы() Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.МобильноеПриложение.ЭтотУзел());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МобильноеПриложение.Ссылка
	|ИЗ
	|	ПланОбмена.МобильноеПриложение КАК МобильноеПриложение
	|ГДЕ
	|	МобильноеПриложение.Ссылка <> &ЭтотУзел
	|	И НЕ МобильноеПриложение.Активность";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Новый Массив;
	Иначе
		Возврат Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли; 

КонецФункции

// Возвращает структуру, в которой ключи соответствую именам рекивзитов в этой базе
//	а значения - именам реквизитов в базе, принимающей данные
//	Внимание! Имена табличных частей должны совпадать!
//
//	Возвращаемая структура построена по иерархическому принципу:
//		* Справочник
//			* ИмяСправочника
//				* ИмяРеквизита
//				* ИмяТабличнойЧасти
//					* ИмяРеквизитаТабличнойЧасти
//		* Документ
//			* ИмяДокумента
//				* ИмяРеквизита
//				* ИмяТабличнойЧасти
//					* ИмяРеквизитаТабличнойЧасти
//	... и т.д.
Функция СоответствиеРеквизитовЭтойБазы() Экспорт

	Результат = Новый Структура;
	
	Результат.Вставить("Справочник", Новый Структура);
	
	Результат.Вставить("Документ", Новый Структура);
	
	Результат.Документ.Вставить("Доход", Новый Структура);
	Результат.Документ.Доход.Вставить("Доходы", Новый Структура);
	Результат.Документ.Доход.Доходы.Вставить("СуммаДохода", "Сумма");
	Результат.Документ.Доход.Доходы.Вставить("Комментарий", "КомментарийСтроки");
	
	Результат.Документ.Вставить("Перемещение", Новый Структура);
	Результат.Документ.Перемещение.Вставить("ДополнительныеРасходы", Новый Структура);
	Результат.Документ.Перемещение.ДополнительныеРасходы.Вставить("КошелекРасхода", "Кошелек");

	Результат.Документ.Вставить("ОбменВалюты", Новый Структура);
	Результат.Документ.ОбменВалюты.Вставить("СуммаВыдано", "СуммаСписания");
	Результат.Документ.ОбменВалюты.Вставить("СуммаПолучено", "СуммаПоступления");
	Результат.Документ.ОбменВалюты.Вставить("СписаноСУчетомКомиссии", "СписаноСУчетомРасходов");
	Результат.Документ.ОбменВалюты.Вставить("ПолученоСУчетомКомиссии", "ПолученоСУчетомРасходов");
	Результат.Документ.ОбменВалюты.Вставить("ДополнительныеРасходы", Новый Структура);
	Результат.Документ.ОбменВалюты.ДополнительныеРасходы.Вставить("КошелекРасхода", "Кошелек");

	Возврат Результат;
	
КонецФункции

// Возвращает структуру, в которой ключи соответствую именам рекивзитов в базе - источнике данных,
//	а значения - именам реквизитов в  этой базе
//	Внимание! Имена табличных частей должны совпадать!
//
//	Возвращаемая структура построена по иерархическому принципу:
//		* Справочник
//			* ИмяСправочника
//				* ИмяРеквизита
//				* ИмяТабличнойЧасти
//					* ИмяРеквизитаТабличнойЧасти
//		* Документ
//			* ИмяДокумента
//				* ИмяРеквизита
//				* ИмяТабличнойЧасти
//					* ИмяРеквизитаТабличнойЧасти
//	... и т.д.
Функция СоответствиеРеквизитовУзлаИсточника() Экспорт

	Возврат ИнвертироватьСоответствиеИмен(СоответствиеРеквизитовЭтойБазы());
	
КонецФункции

// Возвращает строку, идентифицирующую пространство имен
Функция URIПространстваИмен() Экспорт

	Возврат "http://v8.1c.ru/Money/MobileConfig";

КонецФункции


#КонецОбласти




#Область СлужебныеПроцедурыИФункции

Функция ИнвертироватьСоответствиеИмен(СтруктураОригинал)

	НоваяСтруктура = Новый Структура;
	
	Для Каждого КлючИЗначение Из СтруктураОригинал Цикл
		
		Если ТипЗнч(КлючИЗначение.Значение) = Тип("Структура") Тогда
			НоваяСтруктура.Вставить(КлючИЗначение.Ключ, ИнвертироватьСоответствиеИмен(КлючИЗначение.Значение));
		Иначе
			НоваяСтруктура.Вставить(КлючИЗначение.Значение, КлючИЗначение.Ключ);
		КонецЕсли; 
		
	КонецЦикла; 

	Возврат НоваяСтруктура;
	
КонецФункции
 

#КонецОбласти

