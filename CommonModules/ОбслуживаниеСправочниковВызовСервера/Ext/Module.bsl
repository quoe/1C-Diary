


// Создает элемент или группу справочника по параметрам, полученным из поля ввода.
//
// Параметры:
//  ПараметрыСоздания - Структура - см. функцию ОбщегоНазначенияДеньгиКлиент.ПараметрыСозданияОбъектаПоПолюВвода
//
// Возвращаемое значение:
// 	Ссылка, Неопределено - ссылка на созданный объект или Неопределено.
//
Функция СоздатьОбъектСправочникаИзПоляВвода(Знач ИмяСправочника, Знач ПараметрыСоздания) Экспорт
	
	ЭтоГруппа = Ложь;
	ЗначенияЗаполнения = Неопределено;
	Если ПараметрыСоздания.Свойство("ЗначенияЗаполнения", ЗначенияЗаполнения) Тогда
		ЭтоГруппа = ЗначенияЗаполнения.Свойство("ЭтоГруппа") И ЗначенияЗаполнения.ЭтоГруппа = Истина;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		НовыйОбъект = Справочники[ИмяСправочника].СоздатьГруппу();
	Иначе
		НовыйОбъект = Справочники[ИмяСправочника].СоздатьЭлемент();
	КонецЕсли;
	
	Если ПараметрыСоздания.Свойство("ЗначенияЗаполнения") Тогда
		ПараметрыСоздания.ЗначенияЗаполнения.Вставить("ДополнительныеПараметрыСоздания", ПараметрыСоздания.ДополнительныеПараметрыСоздания);
		НовыйОбъект.Заполнить(ПараметрыСоздания.ЗначенияЗаполнения);
	КонецЕсли;
	
	Попытка
		НовыйОбъект.Записать();
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции

