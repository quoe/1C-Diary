////////////////////////////////////////////////////////////////////////////////
// RESTGoogleКлиентСервер
//	Выполнение и обработка запросов к сервисам Google
//
//
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает данные об аккаунте пользователя 
//
//Параметры:
//	КлючДоступа - Строка - ключ (токен), который проверяется
//	СвойстваПровайдера - Структура или Неопределено - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//						Если Неопределено, заполняются авторматически как возвращаемый параметр для уменьшения серверных вызовов
//
// Возвращаемое значение:
//  Соответствие - Данные об аккаунте пользователя
//
Функция ДанныеОбАккаунте(КлючДоступа, СвойстваПровайдера = Неопределено) Экспорт
	
	ПроверитьСвойстваПровайдера(СвойстваПровайдера);
	
	Ответ = ОтветОбАккаунте(КлючДоступа, СвойстваПровайдера);
	
	Если Ответ = Неопределено Тогда //ОК
		Возврат Неопределено;
	ИначеЕсли Ответ.КодСостояния = 200 Тогда //ОК
		СтрокаДанных = Ответ.ПолучитьТелоКакСтроку();
		ОтветJSON = ПарсерJSON.РаспарситьJSON(СтрокаДанных, 0);
		Возврат ОтветJSON;
	Иначе
		ПолучитьОшибкуПровайдера(Ответ, НСтр("ru = 'Ошибка получения данных об аккаунте пользователя!'"));
	КонецЕсли;
		
	Возврат Неопределено;
	
КонецФункции

// Возвращает проверку доступности ключа доступа (токена) - выполняет запрос на сервер и 
//	анализирует полученный ответ
//
//Параметры:
//	КлючДоступа - Строка - ключ (токен), который проверяется
//	СвойстваПровайдера - Структура или Неопределено - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//						Если Неопределено, заполняются авторматически как возвращаемый параметр для уменьшения серверных вызовов
//
//Возвращаемое значение:
//	Булево или Неопределено - Истина, если токен действителен, Ложь если токен отменен и Неопределено в случае ошибки соединения или сервера
//
Функция КлючДоступаДействителен(КлючДоступа, СвойстваАккаунта, СвойстваПровайдера = Неопределено) Экспорт

	ПроверитьСвойстваПровайдера(СвойстваПровайдера);
	
	// Для проверки выполняем запрос по получению информации об аккаунте
	Ответ = ОтветОбАккаунте(КлючДоступа, СвойстваПровайдера);
	
	Если Ответ = Неопределено Тогда //ОК
		Возврат Ложь;
	ИначеЕсли Ответ.КодСостояния = 200 Тогда //ОК
		Возврат Истина;
	ИначеЕсли  Ответ.КодСостояния = 403 Или Ответ.КодСостояния = 401 Тогда
		// Возможно, истек срок разового токена
		Возврат ОбновитьКлючДоступа(КлючДоступа, СвойстваАккаунта, СвойстваПровайдера);
		
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

// Выполняет запрос на сервер для получения токена по коду доступа
//	Результаты выполнения команды и описания ошибок помещаются в СтруктуруРезультата
//	Значение токера помещается в СтруктуруРезультата.Токен
//
// Параметры:
//	СтруктураРезультата  - Структура - см. RESTКлиентСервер.НоваяСтруктураРезультата()
//  ИмяФайла             - Строка - Путь к файлу или папке, информацию о которых нужно получить
//	СвойстваАккаунта     - Структура - логин и токен доступа к серверу провайдера (см. RESTВызовСервера.НоваяСтруктураПараметровREST())
//	СвойстваПровайдера   - Структура - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//
Процедура ОбменятьКодДоступаНаТокен(СтруктураРезультата, КодДоступа, СвойстваПровайдера) Экспорт

	ТекстЗапроса = "";
	ТекстЗапроса = ТекстЗапроса + "client_id=" + СвойстваПровайдера.Идентификатор;
	ТекстЗапроса = ТекстЗапроса + "&" + "client_secret=" + СвойстваПровайдера.Пароль;
	ТекстЗапроса = ТекстЗапроса + "&" + "code=" + КодДоступа;
	ТекстЗапроса = ТекстЗапроса + "&" + "grant_type=authorization_code";
	ТекстЗапроса = ТекстЗапроса + "&" + "redirect_uri=" + СвойстваПровайдера.CallbackURL;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded; charset=utf-8");
	Заголовки.Вставить("state", СвойстваПровайдера.КонтрольнаяСтрока);
	
	Ответ = RESTКлиентСервер.ВыполнитьHTTPЗапрос("POST", СвойстваПровайдера.СерверAPI, "oauth2/v4/token", ТекстЗапроса, , Заголовки, Истина);
	ОбработатьHTTPОтвет(Ответ, СтруктураРезультата, 200);
	
	Если СтруктураРезультата.ВыполненоУспешно Тогда
		
		ПараметрыРезультата = RESTКлиентСервер.СоответствиеИзHTTPОтвета(Ответ);
		СтруктураРезультата.Вставить("Токен", ПараметрыРезультата.Получить("access_token"));
		
		ТокенВосстановления = ПараметрыРезультата.Получить("refresh_token");
		Если ТокенВосстановления <> Неопределено Тогда
			RESTВызовСервера.ЗаписатьДополнительныйПараметрНастройки(ПредопределенноеЗначение("Перечисление.ТипыПровайдеровREST.Google"), "ТокенВосстановления", ТокенВосстановления);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Выполняет запрос на сервер провайдера для получения информации об аккаунте
//	Результат сохраняется в настройках
//
//Параметры:
//	СтруктураРезультата  - Структура - см. RESTКлиентСервер.НоваяСтруктураРезультата()
//	СвойстваАккаунта     - Структура - логин и токен доступа к серверу провайдера (см. RESTВызовСервера.НоваяСтруктураПараметровREST())
//				Значения этого параметра будут обновлены после выполнения запроса на сервер
//	СвойстваПровайдера    - Структура - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//
Процедура ОбновитьСвойстваАккаунта(СтруктураРезультата, СвойстваАккаунта, СвойстваПровайдера) Экспорт
	
	Ответ = ОтветОбАккаунте(СвойстваАккаунта.КлючАвторизации, СвойстваПровайдера);
	ОбработатьHTTPОтвет(Ответ, СтруктураРезультата, 200);
	
	Если СтруктураРезультата.ВыполненоУспешно Тогда
		
		ОписаниеАккаунта = RESTКлиентСервер.СоответствиеИзHTTPОтвета(Ответ);
		ОписаниеПользователя = ОписаниеАккаунта.Получить("user");
		Если ТипЗнч(ОписаниеПользователя) = Тип("Соответствие") Тогда
			СвойстваАккаунта.ПредставлениеАккаунта = ОписаниеПользователя.Получить("emailAddress");
			СвойстваАккаунта.Вставить("СсылкаНаФотоПользователя", ОписаниеПользователя.Получить("photoLink"));
		Иначе
			СвойстваАккаунта.ПредставлениеАккаунта = ОписаниеАккаунта.Получить("email");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Получает информацию об указанных ресурсах
//	Результаты выполнения команды и описания ошибок помещаются в СтруктуруРезультата
//	Информация о ресурсе помещается в СтруктуруРезультата.ИнформацияОРесурсе
//
// Параметры:
//	СтруктураРезультата      - Структура - см. RESTКлиентСервер.НоваяСтруктураРезультата()
//  ИмяФайла             - Строка - Путь к файлу или папке, информацию о которых нужно получить
//	СвойстваАккаунта     - Структура - логин и токен доступа к серверу провайдера (см. RESTВызовСервера.НоваяСтруктураПараметровREST())
//	СвойстваПровайдера   - Структура - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//
Процедура ПолучитьИнформациюОРесурсе(СтруктураРезультата, ИмяФайла, СвойстваАккаунта, СвойстваПровайдера, Количество = Неопределено, Смещение = Неопределено, Сортировка = Неопределено) Экспорт
	
	СтруктураРезультата.Результат.Вставить("Существует", Ложь);
	СтруктураРезультата.Результат.Вставить("ИнформацияОРесурсе", Неопределено);
	
	ОписаниеРесурса = ИнформацияОРесурсе(ИмяФайла, СвойстваАккаунта, СвойстваПровайдера);
	Если ОписаниеРесурса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРезультата.ВыполненоУспешно = Истина;
	СтруктураРезультата.Результат.Вставить("Существует", ОписаниеРесурса["files"].Количество() > 0);
	СтруктураРезультата.Результат.Вставить("ИнформацияОРесурсе", ОписаниеРесурса);
	
КонецПроцедуры

// Загружает в облачное хранилище указанный файл из локальной файловой системы
//		Вызывается из одноименной функции модуля RESTКлиентСервер. Прямой вызов не приветствуется
//
// Параметры:
//	СтруктураРезультата - Структура - см. RESTКлиентСервер.НоваяСтруктураРезультата()
//	ИмяФайлаИсточника    - Строка - Путь к файлу, который надо закачать в облако (Источник)
//	ИмяФайлаНазначения   - Строка - Путь в облаке, куда следует записать файл (Приемник)
//	СвойстваАккаунта - Структура - логин и токен доступа к серверу провайдера (см. RESTВызовСервера.НоваяСтруктураПараметровREST())
//	СвойстваПровайдера - Структура - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//
Процедура ЗагрузитьФайлВОблако(СтруктураРезультата, Знач ИмяФайлаИсточника, Знач ИмяФайлаНазначения, СвойстваАккаунта, СвойстваПровайдера) Экспорт
	
	Если Не КлючДоступаПодтвержденНаСервере(СтруктураРезультата, СвойстваАккаунта, СвойстваПровайдера) Тогда
		Возврат;
	КонецЕсли;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-type", "application/json; charset=utf-8");  
	Заголовки.Вставить("Authorization", "Bearer " + СвойстваАккаунта.КлючАвторизации);
	
	ИмяФайлаНазначения = ИмяФайлаБезРазделителя(ИмяФайлаНазначения);
	ОписаниеРесурса = ИнформацияОРесурсе(ИмяФайлаНазначения, СвойстваАккаунта, СвойстваПровайдера);
	
	Если ОписаниеРесурса = Неопределено Или ОписаниеРесурса["files"].Количество() = 0 Тогда
		// Создание пустого файла с нужным именем для получения его идентификатора и mimeType
		АдресРесурса = "drive/v3/files";
		
		ТекстЗапроса = "{
				|	""parents"": [ ""appDataFolder"" ],
				|	""name"": """+ ИмяФайлаНазначения + """
				|}";
		
		Ответ = RESTКлиентСервер.ВыполнитьHTTPЗапрос("POST", СвойстваПровайдера.СерверРаботыСКонтентом, АдресРесурса, ТекстЗапроса, , Заголовки, Истина);
		ОбработатьHTTPОтвет(Ответ, СтруктураРезультата, 200);
		
		Если Не СтруктураРезультата.ВыполненоУспешно Тогда
			Возврат ;
		КонецЕсли;
		
		СвойстваФайла = RESTКлиентСервер.СоответствиеИзHTTPОтвета(Ответ);
		
	Иначе
		
		СвойстваФайла = ОписаниеРесурса["files"][0];
		
	КонецЕсли;
	
	// загрузка файла по его идентификатору
	Заголовки.Вставить("Content-Type", СвойстваФайла["mimeType"]);
	
	АдресРесурса = "upload/drive/v3/files/" + СвойстваФайла["id"] + "?uploadType=media";
	
	Ответ = RESTКлиентСервер.ВыполнитьHTTPЗапрос("PATCH", СвойстваПровайдера.СерверРаботыСКонтентом, АдресРесурса, , ИмяФайлаИсточника, Заголовки, Истина);
	ОбработатьHTTPОтвет(Ответ, СтруктураРезультата, 200);
	
КонецПроцедуры

// Скачать файл из облака
//
// Параметры:
//	СтруктураРезультата - Структура - см. RESTКлиентСервер.НоваяСтруктураРезультата()
//	ИмяФайлаИсточника    - Строка - Путь к файлу, который надо скачать из облака (Источник)
//	ИмяФайлаНазначения   - Строка - Путь в локальной ФС, куда следует записать файл (Приемник)
//	СвойстваАккаунта - Структура - логин и токен доступа к серверу провайдера (см. RESTВызовСервера.НоваяСтруктураПараметровREST())
//	СвойстваПровайдера - Структура - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//
Процедура СкачатьФайлИзОблака(СтруктураРезультата, Знач ИмяФайлаИсточника, Знач ИмяФайлаНазначения, СвойстваАккаунта, СвойстваПровайдера) Экспорт
	
	Если Не КлючДоступаПодтвержденНаСервере(СтруктураРезультата, СвойстваАккаунта, СвойстваПровайдера) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайлаИсточника = ИмяФайлаБезРазделителя(ИмяФайлаИсточника);
	ОписаниеРесурса = ИнформацияОРесурсе(ИмяФайлаИсточника, СвойстваАккаунта, СвойстваПровайдера);
	Если ОписаниеРесурса = Неопределено Или ОписаниеРесурса["files"].Количество() = 0 Тогда
		
		СтруктураРезультата.КодСостояния = 404;
		СтруктураРезультата.ОписаниеОшибки = "Файл """ + ИмяФайлаИсточника + """ не найден в Google Drive";
		СтруктураРезультата.ПредставлениеОшибки = "Файл не найден";
		Возврат;
		
	КонецЕсли;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Authorization", "Bearer " + СвойстваАккаунта.КлючАвторизации);
	
	СвойстваФайла = ОписаниеРесурса["files"][0];
	
	Заголовки.Вставить("Content-Type", СвойстваФайла["mimeType"]);
	
	АдресРесурса = "drive/v3/files/" + СвойстваФайла["id"] + "?alt=media";
	Ответ = RESTКлиентСервер.ВыполнитьHTTPЗапрос("GET", СвойстваПровайдера.СерверРаботыСКонтентом, АдресРесурса, , , Заголовки, Истина, ИмяФайлаНазначения);
	ОбработатьHTTPОтвет(Ответ, СтруктураРезультата, 200);
	
КонецПроцедуры

// Удалить файл/папку 
//
// Параметры:
//	СтруктураРезультата - Структура - см. RESTКлиентСервер.НоваяСтруктураРезультата()
//	ИмяФайла    - Строка - Путь к файлу, который надо удалить из облака 
//	СвойстваАккаунта - Структура - логин и токен доступа к серверу провайдера (см. RESTВызовСервера.НоваяСтруктураПараметровREST())
//	СвойстваПровайдера - Структура - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//
Процедура УдалитьФайлИзОблака(СтруктураРезультата, Знач ИмяФайла, СвойстваАккаунта, СвойстваПровайдера) Экспорт
	
	Если Не КлючДоступаПодтвержденНаСервере(СтруктураРезультата, СвойстваАккаунта, СвойстваПровайдера) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайла = ИмяФайлаБезРазделителя(ИмяФайла);
	ОписаниеРесурса = ИнформацияОРесурсе(ИмяФайла, СвойстваАккаунта, СвойстваПровайдера);
	Если ОписаниеРесурса = Неопределено Или ОписаниеРесурса["files"].Количество() = 0 Тогда
		
		СтруктураРезультата.КодСостояния = 404;
		СтруктураРезультата.ОписаниеОшибки = "Файл """ + ИмяФайла + """ не найден в Google Drive";
		СтруктураРезультата.ПредставлениеОшибки = "Файл не найден";
		Возврат;
		
	КонецЕсли;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-type", "application/json; charset=utf-8");  
	Заголовки.Вставить("client_id", СвойстваПровайдера.Идентификатор); 
	Заголовки.Вставить("client_secret", СвойстваПровайдера.Пароль); 
	Заголовки.Вставить("Authorization", "Bearer " + СвойстваАккаунта.КлючАвторизации);
	
	Для каждого СвойстваФайла Из ОписаниеРесурса["files"] Цикл
		АдресРесурса = "drive/v3/files/" + СвойстваФайла["id"];
		Ответ = RESTКлиентСервер.ВыполнитьHTTPЗапрос("DELETE", СвойстваПровайдера.СерверРаботыСКонтентом, АдресРесурса, , , Заголовки, Истина);
		ОбработатьHTTPОтвет(Ответ, СтруктураРезультата, 204); 
	КонецЦикла;

КонецПроцедуры

// Получает список файлов, существующих в каталоге облачного сервиса и соответствующих указанной маске 
//	Результаты выполнения команды и описания ошибок помещаются в СтруктуруРезультата
//
// Параметры:
//	СтруктураРезультата  - Структура - см. RESTКлиентСервер.НоваяСтруктураРезультата()
//  Маска                - Строка - часть имени или расширения. Символы подстановки не используются. Если пусто - все файлы и каталоги
//  Путь                 - Строка - каталог, в котором нужно выполнять проверку. Если пусто (по умолчанию) - каталог приложения
//  ВключаяПодчиненные   - Булево - искать ли в подчиненных каталогах или только в указанном
//	СвойстваАккаунта     - Структура - логин и токен доступа к серверу провайдера (см. RESTВызовСервера.НоваяСтруктураПараметровREST())
//	СвойстваПровайдера    - Структура - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//
Процедура СисокФайловВОблаке(СтруктураРезультата, Маска, Путь = "", ВключаяПодчиненные = Истина, СвойстваАккаунта, СвойстваПровайдера) Экспорт
	
	// В данной версии не используется
	
КонецПроцедуры

// Проверяет наличе указанного файла/каталога в облачном хранилище
//	Результаты выполнения команды и описания ошибок помещаются в СтруктуруРезультата
//
// Параметры:
//	СтруктураРезультата  - Структура - см. RESTКлиентСервер.НоваяСтруктураРезультата()
//  ИмяКаталога          - Строка - Путь к создаваемому каталогу
//	СвойстваАккаунта     - Структура - логин и токен доступа к серверу провайдера (см. RESTВызовСервера.НоваяСтруктураПараметровREST())
//	СвойстваПровайдера    - Структура - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//
Процедура СоздатьКаталогВОблаке(СтруктураРезультата, Знач ИмяКаталога, СвойстваАккаунта, СвойстваПровайдера) Экспорт
	
	Если Не КлючДоступаПодтвержденНаСервере(СтруктураРезультата, СвойстваАккаунта, СвойстваПровайдера) Тогда
		Возврат;
	КонецЕсли;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Authorization", "Bearer " + СвойстваАккаунта.КлючАвторизации);
	Заголовки.Вставить("Content-Type", "application/json");
	ИмяКаталога = ИмяФайлаБезРазделителя(ИмяКаталога);
	
	Адрес = "drive/v3/files";
	
	ТекстЗапроса = "{
			|	""parents"": [ 'appDataFolder'],
			|	""name"": '"+ ИмяКаталога + "',
			|	""mimeType"": 'application/vnd.google-apps.folder'
			|}";
	
	Ответ = RESTКлиентСервер.ВыполнитьHTTPЗапрос("POST", СвойстваПровайдера.СерверAPI, Адрес, ТекстЗапроса, , Заголовки, Истина);
	ОбработатьHTTPОтвет(Ответ, СтруктураРезультата, 200);
	
КонецПроцедуры

// Отменяет атворизацию приложения в облачном сервисе
//	Результаты выполнения команды и описания ошибок помещаются в СтруктуруРезультата
//
// Параметры:
//	СтруктураРезультата  - Структура - см. RESTКлиентСервер.НоваяСтруктураРезультата()
//	СвойстваАккаунта     - Структура - логин и токен доступа к серверу провайдера (см. RESTВызовСервера.НоваяСтруктураПараметровREST())
//	СвойстваПровайдера    - Структура - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//
Процедура ОтменитьАвторизациюВОблаке(СтруктураРезультата, СвойстваАккаунта, СвойстваПровайдера) Экспорт
	
	Заголовки = Новый Соответствие;
	
	Адрес = "o/oauth2/revoke?token={" + СвойстваАккаунта.КлючАвторизации + "}";
	
	ТекстЗапроса = "";
	
	Ответ = RESTКлиентСервер.ВыполнитьHTTPЗапрос("GET", СвойстваПровайдера.СерверAPI, Адрес, ТекстЗапроса, , Заголовки, Истина);
	ОбработатьHTTPОтвет(Ответ, СтруктураРезультата, 200);
	
	Если СтруктураРезультата.КодСостояния = 200 Тогда
		RESTВызовСервера.ЗаписатьДополнительныйПараметрНастройки(ПредопределенноеЗначение("Перечисление.ТипыПровайдеровREST.Google"), "ТокенВосстановления", Неопределено);
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбработатьОшибку(ОтветСервера, Результат) Экспорт
	
	Если Результат.КодСостояния = 202 Тогда
		Результат.ПредставлениеОшибки = НСтр("ru='Файл принят сервером, но не перенесен непосредственно в Google Drive'"); 
	ИначеЕсли Результат.КодСостояния = 302 Тогда
		Результат.ПредставлениеОшибки = НСтр("ru='Google Drive вернул некорректную ссылку перенаправления'"); 
	ИначеЕсли Результат.КодСостояния = 413 Тогда
		Результат.ПредставлениеОшибки = НСтр("ru='слишком большой файл'"); 
	ИначеЕсли Результат.КодСостояния = 500 Тогда
		Результат.ПредставлениеОшибки = НСтр("ru='Внутренняя ошибка на сервере Google Drive. Повторите попытку позже'"); 
	ИначеЕсли Результат.КодСостояния = 507 Тогда
		Результат.ПредставлениеОшибки = НСтр("ru='Не хватает места на Google Drive'"); 
		
	ИначеЕсли ОтветСервера <> Неопределено Тогда
		
		ПоляОтвета  = RESTКлиентСервер.СоответствиеИзHTTPОтвета(ОтветСервера);
		
		ТекстОшибки = "";
		Инфо = ПоляОтвета["error"];
		
		Если ТипЗнч(Инфо) = Тип("Соответствие") Тогда
			
			Если ТипЗнч(Инфо["errors"]) = Тип("Соответствие") Тогда
				Для Каждого Элемент Из Инфо["errors"] Цикл
					ТекстОшибки = ТекстОшибки +  "Параметр: " + Строка(Элемент.Ключ) + ", значение: " + Строка(Элемент.Значение);
				КонецЦикла;
			Иначе
				Для Каждого Элемент Из Инфо Цикл
					ТекстОшибки = ТекстОшибки +  "Параметр: " + Строка(Элемент.Ключ) + ", значение: " + Строка(Элемент.Значение);
				КонецЦикла;
			КонецЕсли;
			ТекстОшибки = ТекстОшибки + ".";
			
		ИначеЕсли ТипЗнч(Инфо) = Тип("Строка") Тогда
			ТекстОшибки = Инфо;
		ИначеЕсли Инфо = Неопределено И ПоляОтвета["ТекстОтвета"] <> Неопределено Тогда
			ТекстОшибки = ПоляОтвета["ТекстОтвета"];
		КонецЕсли;

		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ТекстОшибки = RESTКлиентСервер.UnicodeВСтроку(ТекстОшибки);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "\""", """");
			Результат.ОписаниеОшибки = ТекстОшибки;
			Результат.ПредставлениеОшибки = НСтр("ru='Сервер вернул сообщение об ошибке'");
			Возврат;
		КонецЕсли;
		
		Результат.ОписаниеОшибки = ОтветСервера.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
		Результат.ПредставлениеОшибки = НСтр("ru='Неизвестная ошибка'");
		Возврат;
		
	КонецЕсли;
	
	Результат.ОписаниеОшибки = "Ответ сервера не является объектом";
	Результат.ПредставлениеОшибки = НСтр("ru='Неизвестная ошибка'");
	
КонецПроцедуры




#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Выполняет обработку HTTP-ответа сервера в соответствии с заданными параметрами
//	Если код состояния соответствует кодам успешного завершения, в структуру овтета будет помещен флаг Успеха
//	иначе - структура будет заполнена ифнформацие об ошибке
//
//Параметры:
//	СтруктураРезультата - Структура - см. НоваяСтруктураРезультата()
//	ОтветСервера - HTTPОтвет
//	КодыУспешногоЗавершения - Строка - коды ответа сервера (через запятую), соответствующие успешному выполнению запроса
//
Процедура ОбработатьHTTPОтвет(ОтветСервера, Результат, КодУспеха1, КодУспеха2 = Неопределено, КодУспеха3 = Неопределено)
	
	Если ОтветСервера = Неопределено Тогда
		Результат.ВыполненоУспешно = Ложь;
		Результат.КодСостояния = -100;
		Результат.ПредставлениеОшибки = НСтр("ru='Программная ошибка'"); 
		Возврат;
	КонецЕсли;
	
	Результат.КодСостояния = ОтветСервера.КодСостояния;
	Результат.ВыполненоУспешно = (ОтветСервера.КодСостояния = КодУспеха1 
			Или (КодУспеха2 <> Неопределено И ОтветСервера.КодСостояния = КодУспеха2)
			Или (КодУспеха3 <> Неопределено И ОтветСервера.КодСостояния = КодУспеха3));
	
	Если Не Результат.ВыполненоУспешно Тогда
		ОбработатьОшибку(ОтветСервера, Результат);
	КонецЕсли;
	
	
КонецПроцедуры

// Если СвойстваПровайдера = Неопределено, заполняет актуальными
Процедура ПроверитьСвойстваПровайдера(СвойстваПровайдера) 
	
	Если СвойстваПровайдера = Неопределено Тогда
		СвойстваПровайдера = RESTКлиентСервер.СвойстваПровайдера(ПредопределенноеЗначение("Перечисление.ТипыПровайдеровREST.Google"));
	КонецЕсли;
	
КонецПроцедуры

// Парсит ответ от сервера Google Drive и генерирует исключение, или возвращает строку с описанием ошибки
//
// Параметры:
//  ОтветСервера	 - HTTPОтвет  - Ответ сервера, полученного в результате запроса
//  ТекстСообщения   - Строка - Текст сообщения, который будет выводится с информацией об ошибке
//  ГенерироватьИсключение - Булево - Если = Истина, то вызвает исключение с содержанием ошибки
//
// Возвращаемое значение:
//   Строка - В случае, если ГенерироватьИсключение = Ложь, возвращает описание ошибки
//
Функция ПолучитьОшибкуПровайдера(Знач ОтветСервера, Знач ТекстСообщения, Знач ГенерироватьИсключение = Истина)
	
	Текст = ТекстСообщения;
	
	Если ОтветСервера <> Неопределено Тогда
		
		Данные = ОтветСервера.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
		
		Если НЕ ПустаяСтрока(Данные) Тогда
			
			Если СтрНайти(Данные, "{") = 1 Тогда
				
				ОтветJSON = ПарсерJSON.РаспарситьJSON(Данные, 0);
				ОтветJSON = ?(ОтветJSON = Неопределено, Новый Соответствие, ОтветJSON);
				
				Инфо = ОтветJSON["error_summary"];
				
				ТекстОшибки = "";
				Если ТипЗнч(Инфо) = Тип("Соответствие") Тогда
					Для Каждого Элемент Из Инфо Цикл
						ТекстОшибки = ТекстОшибки +  "Параметр: " + Строка(Элемент.Ключ) + ", значение: " + Строка(Элемент.Значение);
					КонецЦикла;
					ТекстОшибки = ТекстОшибки + ".";
				ИначеЕсли ТипЗнч(Инфо) = Тип("Строка") Тогда
					ТекстОшибки = Инфо;
				КонецЕсли;
				
				ТекстОшибки = ОбменМобильноеПриложениеВызовСервера.UnicodeВСтроку(ТекстОшибки);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "\""", "`");
				Текст = Текст + " " + ТекстОшибки;
				
			Иначе
				Текст = Текст + " " + Данные;
			КонецЕсли;
			
		КонецЕсли;
		
		Текст = Текст + " Код ошибки: " + ОтветСервера.КодСостояния;
		
	КонецЕсли;
	
	Если ГенерироватьИсключение Тогда
		ВызватьИсключение Текст;
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

Функция ИнформацияОРесурсе(ИмяФайла, СвойстваАккаунта, СвойстваПровайдера) 

	СтруктураРезультата = RESTКлиентСервер.НоваяСтруктураОтвета("Получение информации о ресурсе");
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("client_id", СвойстваПровайдера.Идентификатор); 
	Заголовки.Вставить("client_secret", СвойстваПровайдера.Пароль); 
	Заголовки.Вставить("Authorization", "Bearer " + СвойстваАккаунта.КлючАвторизации);
	
	АдресРесурса = "drive/v3/files";
	
	//СтрокаПараметров = "q=" + RESTВызовСервера.КодироватьСтрокуКакURL("name='" + ИмяФайла + "' and 'appDataFolder' in parents");
	СтрокаПараметров = "q=" + RESTВызовСервера.КодироватьСтрокуКакURL("name='" + ИмяФайла + "'");
	СтрокаПараметров = СтрокаПараметров + "&spaces=appDataFolder";
	СтрокаПараметров = СтрокаПараметров + "&orderBy=" + RESTВызовСервера.КодироватьСтрокуКакURL("modifiedTime desc,name");
	
	ТекстЗапроса = Неопределено;
		//через тело запроса не обрабатываются условия поиска
	
	Ответ = RESTКлиентСервер.ВыполнитьHTTPЗапрос("GET", СвойстваПровайдера.СерверРаботыСКонтентом, 
				АдресРесурса + "?" + СтрокаПараметров, ТекстЗапроса, , Заголовки, Истина);
	ОбработатьHTTPОтвет(Ответ, СтруктураРезультата, 200);
	
	Если Не СтруктураРезультата.ВыполненоУспешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = RESTКлиентСервер.СоответствиеИзHTTPОтвета(Ответ);
	Если Результат = Неопределено Или Результат["files"] = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат Результат;
	КонецЕсли;

КонецФункции

Функция ОтветОбАккаунте(КлючДоступа, СвойстваПровайдера) 

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-type", "application/json; charset=utf-8");  
	Заголовки.Вставить("Authorization", "Bearer " + КлючДоступа); 
	
	Возврат RESTКлиентСервер.ВыполнитьHTTPЗапрос("GET", СвойстваПровайдера.СерверAPI
				, "drive/v3/about?fields=user,storageQuota", , , Заголовки, Истина);
				
КонецФункции

Функция ИмяФайлаБезРазделителя(Знач ИмяФайла) 

	Разделитель = "/";
	Возврат ?(Лев(ИмяФайла, 1) = Разделитель, Сред(ИмяФайла, 2), ИмяФайла);

КонецФункции

Функция ОбновитьКлючДоступа(КлючДоступа, СвойстваАккаунта, СвойстваПровайдера)

	Если СвойстваАккаунта = Неопределено Тогда
		СвойстваАккаунта = RESTВызовСервера.НастройкиАккаунтаИзБезопасногоХранилища(ПредопределенноеЗначение("Перечисление.ТипыПровайдеровREST.Google"));
	КонецЕсли;
	Если СвойстваАккаунта = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;

	МаркерВосстановления = RESTВызовСервера.ДополнительныйПараметрНастройки(ПредопределенноеЗначение("Перечисление.ТипыПровайдеровREST.Google"), "ТокенВосстановления", Неопределено);
	
	Если Не ЗначениеЗаполнено(МаркерВосстановления) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстЗапроса = "";
	ТекстЗапроса = ТекстЗапроса + "client_id=" + СвойстваПровайдера.Идентификатор;
	ТекстЗапроса = ТекстЗапроса + "&" + "client_secret=" + СвойстваПровайдера.Пароль;
	ТекстЗапроса = ТекстЗапроса + "&" + "refresh_token=" + МаркерВосстановления;
	ТекстЗапроса = ТекстЗапроса + "&" + "grant_type=refresh_token";
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded; charset=utf-8");
	
	СтруктураРезультата = RESTКлиентСервер.НоваяСтруктураОтвета("Обновление маркера доступа");
	Ответ = RESTКлиентСервер.ВыполнитьHTTPЗапрос("POST", СвойстваПровайдера.СерверAPI, "oauth2/v4/token", ТекстЗапроса, , Заголовки, Истина);
	ОбработатьHTTPОтвет(Ответ, СтруктураРезультата, 200);
	
	Если СтруктураРезультата.ВыполненоУспешно Тогда
		
		ПараметрыРезультата = RESTКлиентСервер.СоответствиеИзHTTPОтвета(Ответ);
		КлючДоступа = ПараметрыРезультата.Получить("access_token"); // обновляем значение параметра
		СвойстваАккаунта.КлючАвторизации = КлючДоступа;
		RESTВызовСервера.ОбновитьСвойстваАккаунта(СвойстваАккаунта);
		
		Возврат ЗначениеЗаполнено(КлючДоступа);
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Поскольку токен гугла живет ограниченное время, его проверку следует выполнять перед каждым запросом
//
//Параметры:
//	СтруктураРезультата      - Структура - см. RESTКлиентСервер.НоваяСтруктураРезультата()
//	СвойстваАккаунта         - Структура - логин и токен доступа к серверу провайдера (см. RESTВызовСервера.НоваяСтруктураПараметровREST())
//	СвойстваПровайдера       - Структура - сервера, адреса и другие свойства провайдера (см. RESTКлиентСервер.СвойстваПровайдера())
//
//Возвращаемое значение:
//	Булево - Истина, если токен актуален и Ложь, если его не удалось обновить
//
Функция КлючДоступаПодтвержденНаСервере(СтруктураРезультата, СвойстваАккаунта, СвойстваПровайдера) 

	КлючДействителен = КлючДоступаДействителен(СвойстваАккаунта.КлючАвторизации, СвойстваАккаунта, СвойстваПровайдера);
	Если Не КлючДействителен Тогда
		// Запись в структуру ответа
		СтруктураРезультата.КодСостояния = 401;
		СтруктураРезультата.ОписаниеОшибки = "Токен доступа не подтвержден на сервере";
		СтруктураРезультата.ПредставлениеОшибки = НСтр("ru='Приложение не авторизовано. Возможно, истек срок его авторизации или авторизаця была отменена на сервере'");
	КонецЕсли;
	
	Возврат КлючДействителен;

КонецФункции
 

#КонецОбласти