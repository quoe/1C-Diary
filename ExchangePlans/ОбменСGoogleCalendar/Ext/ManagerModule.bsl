#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция - Узел для календаря Google
// Возвращает ссылку на узел плана обмена, соответствующий календарю Google
// В случае отсутствия создаёт новый узел и возвращает ссылку на него
//
// Параметры:
//  КалендарьСотрудника - СправочникСсылка.КалендариСотрудника
// 
// Возвращаемое значение:
//  ПланОбменаСсылка.ОбменСGoogleCalendar - ссылка на узел плана обмена с Google Calendar
//
Функция УзелДляКалендаряGoogle(КалендарьСотрудника) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОбменСGoogleCalendar.Ссылка
	|ИЗ
	|	ПланОбмена.ОбменСGoogleCalendar КАК ОбменСGoogleCalendar
	|ГДЕ
	|	НЕ ОбменСGoogleCalendar.ЭтотУзел
	|	И ОбменСGoogleCalendar.КалендарьСотрудника = &КалендарьСотрудника");
	Запрос.УстановитьПараметр("КалендарьСотрудника", КалендарьСотрудника);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ПланыОбменаОбъект = ПланыОбмена.ОбменСGoogleCalendar.СоздатьУзел();
		ПланыОбменаОбъект.УстановитьНовыйКод();
		ПланыОбменаОбъект.КалендарьСотрудника = КалендарьСотрудника;
		ПланыОбменаОбъект.Наименование = КалендарьСотрудника;
		ПланыОбменаОбъект.Записать();
		Возврат ПланыОбменаОбъект.Ссылка;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

#КонецОбласти

#КонецЕсли