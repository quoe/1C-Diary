#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьЗадачи() Экспорт
	пНастройкиКомпоновщика = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);	
	
	ТЗЗадачи = ПолучитьТЗЗадачи(пНастройкиКомпоновщика);
	ТЧЗадачи.Загрузить(ТЗЗадачи);
КонецПроцедуры 

Функция ПолучитьТЗЗадачи(НастройкиКомпоновщика) Экспорт 
	ТЗЗадачи = Новый ТаблицаЗначений;
	СхемаКомпоновкиДанныхКонсоли = ПолучитьМакет("СхемаКомпоновкиДанных");
	
	ИсполняемыеНастройки = НастройкиКомпоновщика;
	
	МассивВыбранныхСтатусовКолонок = Новый Массив();
	Для каждого СтрокаТЧНастройкиКолонок из ТЧНастройкиКолонок цикл
		Если НЕ СтрокаТЧНастройкиКолонок.Видимость Тогда
			Продолжить;
		Конецесли;
		МассивВыбранныхСтатусовКолонок.Добавить(СтрокаТЧНастройкиКолонок.Статус);
	Конеццикла;
	
	// + #225 Урянский Д. 2020-07-06
	Обработки.узКанбанДоска.УстановитьПараметр(ИсполняемыеНастройки, 
		"Наблюдатель", Наблюдатель);
	
	Обработки.узКанбанДоска.УстановитьПараметр(ИсполняемыеНастройки, 
		"ИспользоватьОтборПоНаблюдателю", Истина, ЗначениеЗаполнено(Наблюдатель));
	
	Обработки.узКанбанДоска.УстановитьПараметр(ИсполняемыеНастройки, 
		"ТекущаяДата", ТекущаяДатаСеанса());
	
	Обработки.узКанбанДоска.УстановитьПараметр(ИсполняемыеНастройки, 
		"МассивВыбранныхСтатусовКолонок", МассивВыбранныхСтатусовКолонок);
	
	ИсполняемыеНастройки.Выбор.Элементы.Очистить();
	
	Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "Задача");
	Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "ЗадачаПредставление");
	Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "Статус");
	Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "КороткоеИмя");
	Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "ОсновнаяЗадача");
	Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "ОсновнаяЗадачаПредставление");
	Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "НомерЗадачиПредставление");
	Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "ЦветДляЗадачиЧисло");
	Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "ИзмененЦветПоУмолчанию");
	
	Если Показывать3СтрокуВКарточкеЗадачи Тогда
		
		Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "КороткоеИмяЗаказчик", 
			ПоказыватьЗаказчика);
		
		Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "КоличествоПисем",
			ОтображениеПисемПоЗадачам = Перечисления.узОтображениеПисемПоЗадачамНаДоске.ЗначениеИКартинка);
		
		Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "ЕстьПисьма",
			ОтображениеПисемПоЗадачам = Перечисления.узОтображениеПисемПоЗадачамНаДоске.Картинка);
		
		Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "КоличествоФайлов",
			ОтображениеФайловЗадач = Перечисления.узОтображениеФайловЗадачНаДоске.ЗначениеИКартинка);
			
		Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "ЕстьФайлы",
			ОтображениеФайловЗадач = Перечисления.узОтображениеФайловЗадачНаДоске.Картинка);
			
		Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "ЧекЛистВыполнение", 
			ОтображениеЧеклистаЗадач = Перечисления.узОтображениеЧеклистаЗадачНаДоске.Дробь);
			
		Обработки.узКанбанДоска.ДобавитьВыбранноеПоле(ИсполняемыеНастройки, "ЧекЛистВыполнениеПроцент", 
			ОтображениеЧеклистаЗадач = Перечисления.узОтображениеЧеклистаЗадачНаДоске.Процент);
			
	КонецЕсли;	
	// - #225 Урянский Д. 2020-07-06
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанныхКонсоли, ИсполняемыеНастройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	// + #225 Урянский Д. 2020-07-06
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, , , Истина);
	// - #225 Урянский Д. 2020-07-06
	ПроцессорВывода.УстановитьОбъект(ТЗЗадачи);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);	
	
	Возврат ТЗЗадачи;
КонецФункции  

Процедура СменитьСтатусЗадачи(ДопПараметры) Экспорт
	НовыйСтатус = ДопПараметры.НовыйСтатус;
	МассивЗадач = ДопПараметры.МассивЗадач;
	
	Для каждого ЭлМассиваЗадач из МассивЗадач цикл
		СпрОбъект = ЭлМассиваЗадач.ПолучитьОбъект();		
		СпрОбъект.Статус = НовыйСтатус;
		СпрОбъект.Записать();
	Конеццикла;	
	ЗаполнитьЗадачи();
КонецПроцедуры 

Процедура ЗаполнитьТЧНастройкиКолонок() Экспорт
	
	//+ #106 Дзеса Ігор (capitoshko)
	ТЧНастройкиКолонок.Очистить();
	//- #106 Дзеса Ігор (capitoshko) 
	
	пРодитель = ПредопределенноеЗначение("Справочник.узСтатусыЗадачи.ПустаяСсылка");
	ЗагрузитьПодчиненныеЭлементы(пРодитель);
	
КонецПроцедуры 

Процедура ЗагрузитьПодчиненныеЭлементы(пРодитель)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	узСтатусыЗадачи.Ссылка КАК Статус,
	|	узСтатусыЗадачи.ВидимостьПоУмолчанию КАК Видимость,
	|	узСтатусыЗадачи.ИмяПредопределенныхДанных
	|ИЗ
	|	Справочник.узСтатусыЗадачи КАК узСтатусыЗадачи
	|ГДЕ
	|	узСтатусыЗадачи.НеИспользуется = ЛОЖЬ
	|	И узСтатусыЗадачи.Родитель = &Родитель
	|
	|УПОРЯДОЧИТЬ ПО
	|	узСтатусыЗадачи.РеквизитДопУпорядочивания";
	
	Запрос.УстановитьПараметр("Родитель",пРодитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	Конецесли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаТЧНастройкиКолонок = ТЧНастройкиКолонок.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧНастройкиКолонок,Выборка);
		
		ЗагрузитьПодчиненныеЭлементы(Выборка.Статус);
	Конеццикла;
	
	
КонецПроцедуры 

#КонецОбласти


