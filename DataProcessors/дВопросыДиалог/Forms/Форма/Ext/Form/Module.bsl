
&НаКлиенте
Процедура ПолучитьОтвет(Команда)
	
	лПользователь 	= Пользователь; 
	Если НЕ ЗначениеЗаполнено(лПользователь) Тогда
		Возврат;	
	КонецЕсли; 
	
	лРезультат = ПолучитьОтветНаСервере(лПользователь, Вопрос);
	
	Если лРезультат <> Неопределено Тогда
		Ответ 					= лРезультат.Ответ;
		СсылкаНаЗаписьОтвета 	= лРезультат.Ссылка;
	КонецЕсли; 
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПолучитьОтветНаСервере(пАвтор, пВопрос = Неопределено)
	
	лРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	дВопросОтвет.Регистратор КАК Регистратор,
		|	дВопросОтвет.Ответ КАК Ответ
		|ИЗ
		|	РегистрСведений.дВопросОтвет КАК дВопросОтвет
		|ГДЕ
		|	дВопросОтвет.Автор = &Автор
		|	И НЕ дВопросОтвет.Регистратор.Скрытое";
	
	Если пВопрос <> Неопределено Тогда			   
		Запрос.Текст = Запрос.Текст + "
		|	И дВопросОтвет.Вопрос = &Вопрос";
		Запрос.УстановитьПараметр("Вопрос"	, пВопрос);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Автор"	, пАвтор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выгрузить();
		
		лВыборкаКоличество 	= Выборка.Количество();
		
		ГСЧ 			= Новый ГенераторСлучайныхЧисел();
		СлучайноеЧисло 	= ГСЧ.СлучайноеЧисло(0, лВыборкаКоличество - 1);
		
		лСлучайнаяЗапись = Выборка[СлучайноеЧисло];
		
		лСтруктураПараметров = Новый Структура();
		лСтруктураПараметров.Вставить("Ответ"	, лСлучайнаяЗапись.Ответ);
		лСтруктураПараметров.Вставить("Ссылка"	, лСлучайнаяЗапись.Регистратор);

	    лРезультат = лСтруктураПараметров;
	КонецЕсли;  
	
	Возврат лРезультат;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекДата 	= ТекущаяДата();
	ДатаОтвета 	= КонецДня(ТекДата);
	
	лВопросыКоличество = ЗаполнитьВопросыКоличество(ДатаОтвета);
	Если ЗначениеЗаполнено(лВопросыКоличество) Тогда
		ВопросыКоличество.Загрузить(лВопросыКоличество);	
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьВопросыКоличество(пПериод)
	
	лРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	дВопросОтвет.Автор КАК Пользователь,
		|	дВопросОтвет.Вопрос КАК Вопрос,
		|	КОЛИЧЕСТВО(дВопросОтвет.Регистратор) КАК Количество
		|ИЗ
		|	РегистрСведений.дВопросОтвет КАК дВопросОтвет
		|ГДЕ
		|	дВопросОтвет.Период <= &Период
		|
		|СГРУППИРОВАТЬ ПО
		|	дВопросОтвет.Вопрос,
		|	дВопросОтвет.Автор
		|
		|УПОРЯДОЧИТЬ ПО
		|	Количество УБЫВ,
		|	Пользователь,
		|	Вопрос";
	
	Запрос.УстановитьПараметр("Период", пПериод);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		лВыборка 	= РезультатЗапроса.Выгрузить();
		
		Если ЗначениеЗаполнено(лВыборка) Тогда
			лРезультат = лВыборка;
		КонецЕсли; 
	КонецЕсли; 
	 
	
	Возврат лРезультат;
	
КонецФункции // ЗаполнитьВопросыКоличество()


&НаКлиенте
Процедура ВопросыКоличествоПриАктивизацииСтроки(Элемент)
	
	лЭлементТекущиеДанные 	= Элемент.ТекущиеДанные; 
	Если лЭлементТекущиеДанные <> Неопределено Тогда
		Пользователь 	= лЭлементТекущиеДанные.Пользователь;
		Вопрос 			= лЭлементТекущиеДанные.Вопрос;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ВопросыКоличествоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПолучитьОтвет(Неопределено);
	
КонецПроцедуры
 
