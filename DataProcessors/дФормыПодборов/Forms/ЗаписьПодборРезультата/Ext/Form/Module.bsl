
&НаКлиенте
Процедура ПеренестиВЗапись(Команда)
	
	Закрыть(ПодготовитьРезультатКПереносуВЗапись(ВладелецФормы.УникальныйИдентификатор));
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьРезультатКПереносуВЗапись(ИДВладельца)

	АдресТаблицы = ПоместитьВоВременноеХранилище(Результаты.Выгрузить(), ИДВладельца);
	Результат = Новый Структура("АдресТаблицыВыбора", АдресТаблицы);
	
	Возврат Результат;

КонецФункции


&НаКлиенте
Процедура СписокИсторииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыборСтрокиСпискаИстории();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРезультатВСписок(СтруктураВыбора)

	НоваяСтрока 				= Результаты.Добавить();
	Для каждого СтруктураВыбораСтрока Из СтруктураВыбора Цикл
	
		НоваяСтрока[СтруктураВыбораСтрока.Ключ] = СтруктураВыбораСтрока.Значение;
	
	КонецЦикла;
	//НоваяСтрока.Время 			= СтруктураВыбора.Время;
	//НоваяСтрока.ИмяРезультата 	= СтруктураВыбора.ИмяРезультата;
	//НоваяСтрока.Параметр        = СтруктураВыбора.Параметр;
	//НоваяСтрока.Значение        = СтруктураВыбора.Значение;
	
	Элементы.ФормаПеренестиВЗапись.Доступность = Результаты.Количество() > 0;

КонецПроцедуры

&НаКлиенте
Процедура ВыборСтрокиСпискаИстории()

	ТекущаяСтрока = Элементы.СписокИстории.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Время) Тогда
		лВремя = Время; //Время, пришедшее из параметра (т.е. время документа открывшего форму)
	Иначе
		лВремя = ТекущаяСтрока.Время;
	КонецЕсли; 
	
	ПараметрВыбора = Новый Структура("Время, ИмяРезультата, Параметр, Значение", 
		лВремя, ТекущаяСтрока.ИмяРезультата, ТекущаяСтрока.Параметр, ТекущаяСтрока.Значение);
	
	ДобавитьРезультатВСписок(ПараметрВыбора);

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УсловноеОформлениеВажности();
	УсловноеОформлениеВидаЗаписи();
	
	ТекПользователь 	= ПользователиСлужебный.АвторизованныйПользователь();
	дОбщиеФункцииСервер.УстановитьОтборСписка(СписокИстории.Отбор.Элементы, "Автор", ТекПользователь);
	
	Если Параметры.Свойство("Дата") Тогда
		Время = Параметры.Дата;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УсловноеОформлениеВажности()

	//ПрименитьУсловноеОфомление("Важность", ВидСравненияКомпоновкиДанных.Равно, Перечисления.дВажность.Обычное	, "Дата", "Важность_Обычное",, WebЦвета.НейтральноЗеленый);
	дОбщиеФункцииСервер.ПрименитьУсловноеОфомление(СписокИстории.УсловноеОформление.Элементы, "Важность", ВидСравненияКомпоновкиДанных.Равно, Перечисления.дВажность.Важное	, "Период", "Важность_Важное",, WebЦвета.Оранжевый);
	дОбщиеФункцииСервер.ПрименитьУсловноеОфомление(СписокИстории.УсловноеОформление.Элементы, "Важность", ВидСравненияКомпоновкиДанных.Равно, Перечисления.дВажность.Особое	, "Период", "Важность_Особое",, WebЦвета.БледноБирюзовый);
	
КонецПроцедуры

&НаСервере
Процедура УсловноеОформлениеВидаЗаписи()

	//дОбщиеФункцииСервер.ПрименитьУсловноеОфомление(СписокИстории.УсловноеОформление.Элементы, "ВидЗаписи", ВидСравненияКомпоновкиДанных.Равно, Перечисления.дВидЗаписи.Обычный	, "Дата", "ВидЗаписи_Обычный",, WebЦвета.Лосось);
	дОбщиеФункцииСервер.ПрименитьУсловноеОфомление(СписокИстории.УсловноеОформление.Элементы, "ВидЗаписи", ВидСравненияКомпоновкиДанных.Равно, Перечисления.дВидЗаписи.Личный	, "ИмяРезультата", "ВидЗаписи_Личный",, WebЦвета.БледноКрасноФиолетовый);
	дОбщиеФункцииСервер.ПрименитьУсловноеОфомление(СписокИстории.УсловноеОформление.Элементы, "ВидЗаписи", ВидСравненияКомпоновкиДанных.Равно, Перечисления.дВидЗаписи.Рабочий	, "ИмяРезультата", "ВидЗаписи_Рабочий",, WebЦвета.ПесочноКоричневый);
	дОбщиеФункцииСервер.ПрименитьУсловноеОфомление(СписокИстории.УсловноеОформление.Элементы, "ВидЗаписи", ВидСравненияКомпоновкиДанных.Равно, Перечисления.дВидЗаписи.Общий	, "ИмяРезультата", "ВидЗаписи_Общий",, WebЦвета.СветлоНебесноГолубой);
	дОбщиеФункцииСервер.ПрименитьУсловноеОфомление(СписокИстории.УсловноеОформление.Элементы, "ВидЗаписи", ВидСравненияКомпоновкиДанных.Равно, Перечисления.дВидЗаписи.Прочее	, "ИмяРезультата", "ВидЗаписи_Прочее",, WebЦвета.БледноЗолотистый);
	
КонецПроцедуры
