////////////////////////////////////////////////////////////////////////////////
//Обработка.НачальнаяСтраница.Форма.НастройкаИндикатораОстатка
//  Добавление или изменений настроек индикатора остатка
//  
//Параметры формы:
//  
//  
////////////////////////////////////////////////////////////////////////////////


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РазделУчета         = Параметры.РазделУчета;
	ВидОбъектаУчета     = Параметры.ВидОбъектаУчета;
	ЗаголовокИндикатора = Параметры.Заголовок;
	Ключ                = Параметры.Ключ;
	СкрыватьЕслиПусто   = Параметры.СкрыватьЕслиПусто;
	
	Если Не ЗначениеЗаполнено(РазделУчета) Тогда
		РазделУчета = ПланыСчетов.РазделыУчета.Деньги;
		ВидОбъектаУчета = "СвободныеДеньги";
	КонецЕсли;
	
	Если Параметры.ОбъектУчета <> Неопределено Тогда
		ЗаполнитьСписокОбъектомУчета(РазделУчета, Параметры.ОбъектУчета);
	КонецЕсли;
	Если Параметры.ФинансоваяЦель <> Неопределено Тогда
		ФинансоваяЦель = Параметры.ФинансоваяЦель;
	КонецЕсли;
	
	ПереключитьСтраницуРазделаУчета(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФинансоваяЦельПриИзменении(Элемент)
	ОбновитьЗаголовокИндикатора();
КонецПроцедуры

&НаКлиенте
Процедура РазделУчетаПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(РазделУчета) Тогда
		РазделУчета = ПредопределенноеЗначение("ПланСчетов.РазделыУчета.Деньги");
		ВидОбъектаУчета = "СвободныеДеньги";
	КонецЕсли;
	
	Если РазделУчета <> ТекущийРазделУчета Тогда
		ВидОбъектаУчета = "";
	КонецЕсли;
	ТекущийРазделУчета = РазделУчета;
	
	ОбновитьЗаголовокИндикатора();
	ПереключитьСтраницуРазделаУчета(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДенегПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
	ОбновитьЗаголовокИндикатора();
КонецПроцедуры

&НаКлиенте
Процедура СписокКошельковПриИзменении(Элемент)
	ОбновитьЗаголовокИндикатора();
КонецПроцедуры

&НаКлиенте
Процедура СписокДолговПриИзменении(Элемент)
	ОбновитьЗаголовокИндикатора();
КонецПроцедуры

&НаКлиенте
Процедура СписокИмуществаПриИзменении(Элемент)
	ОбновитьЗаголовокИндикатора();
КонецПроцедуры



#КонецОбласти



#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	ВыбратьНаКлиенте();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Форма.Элементы.ФинансоваяЦель.Доступность = Форма.ВидОбъектаУчета = "Накопление";

КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьСписокОбъектомУчета(РазделУчета, МассивСсылок)

	Если РазделУчета = ПланыСчетов.РазделыУчета.Деньги Тогда
		СписокКошельков.ЗагрузитьЗначения(МассивСсылок);
	ИначеЕсли РазделУчета = ПланыСчетов.РазделыУчета.Долги Тогда
		СписокДолгов.ЗагрузитьЗначения(МассивСсылок);
	ИначеЕсли РазделУчета = ПланыСчетов.РазделыУчета.Имущество Тогда
		СписокИмущества.ЗагрузитьЗначения(МассивСсылок);
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПереключитьСтраницуРазделаУчета(Форма)

	Если Форма.РазделУчета = ПредопределенноеЗначение("ПланСчетов.РазделыУчета.Деньги") Тогда
		Форма.Элементы.ГруппаСтраницНастроек.ТекущаяСтраница = Форма.Элементы.СтраницаДеньги;
	ИначеЕсли Форма.РазделУчета = ПредопределенноеЗначение("ПланСчетов.РазделыУчета.Долги") Тогда
		Форма.Элементы.ГруппаСтраницНастроек.ТекущаяСтраница = Форма.Элементы.СтраницаДолги;
	ИначеЕсли Форма.РазделУчета = ПредопределенноеЗначение("ПланСчетов.РазделыУчета.Имущество") Тогда
		Форма.Элементы.ГруппаСтраницНастроек.ТекущаяСтраница = Форма.Элементы.СтраницаИмущество;
	Иначе
		Форма.Элементы.ГруппаСтраницНастроек.ТекущаяСтраница = Форма.Элементы.СтраницаФинансовыйРезультат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНаКлиенте()

	Если Не ЗначениеЗаполнено(ЗаголовокИндикатора) Тогда
		ЗаголовокИндикатора = ПредставлениеПоказателяОстатка(ЭтотОбъект);
	КонецЕсли;
	
	Результат = Новый Структура("ОбъектУчета,ФинансоваяЦель");
	
	Результат.Вставить("Ключ", Ключ);
	Результат.Вставить("РазделУчета",       РазделУчета);
	Результат.Вставить("ВидОбъектаУчета",   ?(ВидОбъектаУчета = "", Неопределено, ВидОбъектаУчета));
	Результат.Вставить("Заголовок",         ЗаголовокИндикатора);
	Результат.Вставить("СкрыватьЕслиПусто", СкрыватьЕслиПусто);
	
	Если РазделУчета = ПредопределенноеЗначение("ПланСчетов.РазделыУчета.Деньги") Тогда
		
		Результат.Вставить("ОбъектУчета", МассивНепустыхЗначенийСписка(СписокКошельков));
		
		Если ВидОбъектаУчета = "Накопление" И ЗначениеЗаполнено(ФинансоваяЦель) Тогда
			Результат.Вставить("ФинансоваяЦель", ФинансоваяЦель);
		КонецЕсли;
		
	ИначеЕсли РазделУчета = ПредопределенноеЗначение("ПланСчетов.РазделыУчета.Долги") Тогда
		
		Результат.Вставить("ОбъектУчета", МассивНепустыхЗначенийСписка(СписокДолгов));
		
	ИначеЕсли РазделУчета = ПредопределенноеЗначение("ПланСчетов.РазделыУчета.Имущество") Тогда
		
		Результат.Вставить("ОбъектУчета", МассивНепустыхЗначенийСписка(СписокИмущества));
		
	КонецЕсли;

	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция МассивНепустыхЗначенийСписка(СписокЗначений)

	Если СписокЗначений.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Массив;
	Для каждого КлючИЗначение Из СписокЗначений Цикл
		Если ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			Результат.Добавить(КлючИЗначение.Значение);
		КонецЕсли;
	КонецЦикла;

	Возврат ?(Результат.Количество() = 0, Неопределено, Результат);
	
КонецФункции

&НаКлиенте
Процедура ОбновитьЗаголовокИндикатора()

	ЗаголовокИндикатора = ПредставлениеПоказателяОстатка(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеПоказателяОстатка(Форма)

	ТекстПредставления = "";
	Если ЗначениеЗаполнено(Форма.ВидОбъектаУчета) Тогда
		ТекстПредставления = ПредставлениеВидаОбъектаУчета(Форма.ВидОбъектаУчета);
	КонецЕсли;
	
	МассивЭлементов = Неопределено;
	Если Форма.РазделУчета = ПредопределенноеЗначение("ПланСчетов.РазделыУчета.Деньги") Тогда 
		
		МассивЭлементов = МассивНепустыхЗначенийСписка(Форма.СписокКошельков);
		
		Если Форма.ВидОбъектаУчета = "Накопление" И ЗначениеЗаполнено(Форма.ФинансоваяЦель) Тогда
			ТекстПредставления = ТекстПредставления + ?(ТекстПредставления = "", "", " ") + Строка(Форма.ФинансоваяЦель);
		КонецЕсли;
		
	ИначеЕсли Форма.РазделУчета = ПредопределенноеЗначение("ПланСчетов.РазделыУчета.Долги") Тогда 
		
		МассивЭлементов = МассивНепустыхЗначенийСписка(Форма.СписокДолгов);
		
	ИначеЕсли Форма.РазделУчета = ПредопределенноеЗначение("ПланСчетов.РазделыУчета.Имущество") Тогда 
		
		МассивЭлементов = МассивНепустыхЗначенийСписка(Форма.СписокИмущества);
		
	КонецЕсли;
	
	Если МассивЭлементов <> Неопределено Тогда
		ТекстПредставления = ТекстПредставления + ?(ТекстПредставления = "", "", ": ");
		Для каждого ЭлементМассива Из МассивЭлементов Цикл
			ТекстПредставления = ТекстПредставления
					+ ?(Прав(ТекстПредставления, 1) = " ", "", "; ") 
					+ ДеньгиКлиентСервер.СокращенноеПредставление(Строка(ЭлементМассива), 12, Ложь);
		КонецЦикла;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстПредставления) Тогда
		ТекстПредставления =  Строка(Форма.РазделУчета);
	КонецЕсли;

	Возврат ТекстПредставления;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеВидаОбъектаУчета(ВидОбъекта)

	СписокВидов = СписокВидовОбъектовУчета();
	ЭлементСписка = СписокВидов.НайтиПоЗначению(ВидОбъекта);
	Если ЭлементСписка = Неопределено Тогда
		Возврат "???";
	Иначе
		Возврат ЭлементСписка.Представление;
	КонецЕсли;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СписокВидовОбъектовУчета()

	Результат = Новый СписокЗначений;
	Результат.Добавить("СвободныеДеньги", НСтр("ru='Свободные деньги'"));
	Результат.Добавить("Накопление", НСтр("ru='Накопления'"));
	Результат.Добавить("КредитныеКарты", НСтр("ru='Кредитные карты'"));
	Результат.Добавить("МыДолжны", НСтр("ru='Мы должны'"));
	Результат.Добавить("НамДолжны", НСтр("ru='Нам должны'"));
	Результат.Добавить("Имущество", НСтр("ru='Имущество'"));
	Результат.Добавить("ФинансовыйРезультат", НСтр("ru='Финансовый результат'"));
	
	Возврат Результат;

КонецФункции



#КонецОбласти

