
Функция ПолучитьМассивНастроекПроксиСерверов() Экспорт
	
	МассивНастроекПроксиСерверов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 5
		|	узПроксиСерверы.Адрес,
		|	узПроксиСерверы.Порт,
		|	узПроксиСерверы.Логин,
		|	узПроксиСерверы.Пароль,
		|	узПроксиСерверы.ОценкаРаботоспособности КАК ОценкаРаботоспособности
		|ИЗ
		|	РегистрСведений.узПроксиСерверы КАК узПроксиСерверы
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОценкаРаботоспособности УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НастройкиПроксиСтруктура = Новый Структура;
		НастройкиПроксиСтруктура.Вставить("Адрес");
		НастройкиПроксиСтруктура.Вставить("Порт");
		НастройкиПроксиСтруктура.Вставить("Логин");
		НастройкиПроксиСтруктура.Вставить("Пароль");
		ЗаполнитьЗначенияСвойств(НастройкиПроксиСтруктура, Выборка);
		МассивНастроекПроксиСерверов.Добавить(НастройкиПроксиСтруктура);
	КонецЦикла;
	
	Возврат МассивНастроекПроксиСерверов;
	
КонецФункции

Процедура ИзменитьОценкуРаботоспособностиПроксиСервера(АдресПрокси, ПортПрокси, ИзменениеОценки = 0) Экспорт

	Запись = РегистрыСведений.узПроксиСерверы.СоздатьМенеджерЗаписи();
    Запись.Адрес = АдресПрокси;
	Запись.Порт = ПортПрокси;
	Запись.Прочитать();
	Если Не Запись.Выбран() Тогда
		Возврат;
	КонецЕсли;	
	
	Запись.ОценкаРаботоспособности = Запись.ОценкаРаботоспособности + ИзменениеОценки;
	Запись.Записать();

КонецПроцедуры

Процедура ЗаполнитьНастройкамиПоУмолчанию() Экспорт
	
	Макет = РегистрыСведений.узПроксиСерверы.ПолучитьМакет("НастройкиПоУмолчанию");
	
	ТЗНастройкиПоУмолчанию= Новый ТаблицаЗначений();
	ТЗНастройкиПоУмолчанию.Колонки.Добавить("Адрес", Новый ОписаниеТипов("Строка"));
	ТЗНастройкиПоУмолчанию.Колонки.Добавить("Порт", Новый ОписаниеТипов("Число"));
	ТЗНастройкиПоУмолчанию.Колонки.Добавить("Логин", Новый ОписаниеТипов("Строка"));
	ТЗНастройкиПоУмолчанию.Колонки.Добавить("Пароль", Новый ОписаниеТипов("Строка"));
	
	Для НомерСтроки = 2 По Макет.ВысотаТаблицы Цикл
		СтрокаТЗНастройкиПоУмолчанию = ТЗНастройкиПоУмолчанию.Добавить();		
		СтрокаТЗНастройкиПоУмолчанию.Адрес = СокрЛП(Макет.Область(НомерСтроки,1).Текст);
		СтрокаТЗНастройкиПоУмолчанию.Порт = Число(СокрЛП(Макет.Область(НомерСтроки,2).Текст));
		СтрокаТЗНастройкиПоУмолчанию.Логин = СокрЛП(Макет.Область(НомерСтроки,3).Текст);
		СтрокаТЗНастройкиПоУмолчанию.Пароль = СокрЛП(Макет.Область(НомерСтроки,4).Текст);
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.узПроксиСерверы.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(ТЗНастройкиПоУмолчанию);
	НаборЗаписей.Записать();	
	
КонецПроцедуры 