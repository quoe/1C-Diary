
//+ #201 Иванов А.Б. 2020-05-23 Изменения от Дениса Урянского @d-hurricane
// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Задача)";
	
КонецПроцедуры //- #201 Иванов А.Б. 2020-05-23 Изменения от Дениса Урянского @d-hurricane

Функция ПолучитьЧасыФактЗаДень(НаДату,Исполнитель) Экспорт
	пЧасыФактЗаДень = 0;
	
	пНачалоПериода = НачалоДня(НаДату);
	пКонецПериодаГраница = Новый Граница(КонецДня(пНачалоПериода),ВидГраницы.Включая);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьЧасыФактЗаДень_ТекстЗапроса();
	
	Запрос.УстановитьПараметр("НачалоПериода",пНачалоПериода);
	Запрос.УстановитьПараметр("КонецПериодаГраница",пКонецПериодаГраница);
	Запрос.УстановитьПараметр("Исполнитель",Исполнитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат пЧасыФактЗаДень;
	Конецесли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	пЧасыФактЗаДень = Выборка.ФактОборот;
	
	Возврат пЧасыФактЗаДень;
КонецФункции 

Функция ПолучитьЧасыФактЗаДень_ТекстЗапроса() 
	Возврат 
	"ВЫБРАТЬ
	|	узФактПоЗадачамОбороты.ФактОборот
	|ИЗ
	|	РегистрНакопления.узФактПоЗадачам.Обороты(&НачалоПериода, &КонецПериодаГраница, , Исполнитель = &Исполнитель) КАК узФактПоЗадачамОбороты
	|";
	
КонецФункции 

Функция ПолучитьТабДокументЧасыЗаДень(НаДату,Исполнитель) Экспорт
	
	ТабДокумент = Новый ТабличныйДокумент;
	
		
	Макет = ПолучитьМакет("МакетЧасыЗаДень");
	
	пДатаДеньИДеньНеделиТекст = Формат(НаДату,"ДФ='dddd dd.MM.yyyy'");
	пДатаДеньИДеньНеделиТекст = Лев(ВРЕГ(пДатаДеньИДеньНеделиТекст),1) + Сред(пДатаДеньИДеньНеделиТекст,2);
	
	ЗаголовокМакета = "Часы за " + пДатаДеньИДеньНеделиТекст;
	
	пЧасыЗаДеньФакт = ПолучитьЧасыФактЗаДень(НаДату,Исполнитель);
	
	Если пЧасыЗаДеньФакт > 0 Тогда
		ВыборкаФактЗаДень = ПолучитьВыборкуФактЗаДень(НаДату,Исполнитель);
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьМакета.Параметры.ДатаДеньИДеньНедели = пДатаДеньИДеньНеделиТекст;	
	ОбластьМакета.Параметры.ЧасыЗаДеньФакт = пЧасыЗаДеньФакт;
	ОбластьМакета.Параметры.Исполнитель = Исполнитель;
	ТабДокумент.Вывести(ОбластьМакета);	
	
	НС = 1;
	Пока пЧасыЗаДеньФакт > 0
		И ВыборкаФактЗаДень.Следующий() Цикл
		
		ОбластьМакета = Макет.ПолучитьОбласть("ОбластьСтрока");
		ОбластьМакета.Параметры.Заполнить(ВыборкаФактЗаДень);
		ОбластьМакета.Параметры.НС = НС;
		ТабДокумент.Вывести(ОбластьМакета);		
		
		НС = НС + 1;
	конеццикла;

	ОбластьМакета = Макет.ПолучитьОбласть("ОбластьПодвал");
	ОбластьМакета.Параметры.ЧасыЗаДеньФакт = пЧасыЗаДеньФакт;
	ТабДокумент.Вывести(ОбластьМакета);	

	ТабДокумент.ОтображатьЗаголовки = Ложь;
	ТабДокумент.ОтображатьСетку = Ложь;
	ТабДокумент.ТолькоПросмотр = Истина;	

	Возврат ТабДокумент;
КонецФункции 

Функция ПолучитьВыборкуФактЗаДень(НаДату,Исполнитель) 
	
	пНачалоПериода = НачалоДня(НаДату);
	пКонецПериода = КонецДня(пНачалоПериода);	
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьВыборкуФактЗаДень_ТекстЗапроса();
	
	Запрос.УстановитьПараметр("НачалоПериода",пНачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",пКонецПериода);
	Запрос.УстановитьПараметр("Исполнитель",Исполнитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Возврат Выборка;	
	
КонецФункции 

Функция ПолучитьВыборкуФактЗаДень_ТекстЗапроса() 
	Возврат 
	"ВЫБРАТЬ
	|	узФактПоЗадачам.Задача.Код КАК НомерЗадачи,
	|	узФактПоЗадачам.Задача,
	|	узФактПоЗадачам.Факт КАК ЧасыФакт,
	|	узФактПоЗадачам.Примечание
	|ИЗ
	|	РегистрНакопления.узФактПоЗадачам КАК узФактПоЗадачам
	|ГДЕ
	|	узФактПоЗадачам.Исполнитель = &Исполнитель
	|	И узФактПоЗадачам.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	узФактПоЗадачам.Период";

	
КонецФункции 